#ifndef NETWORK_S3D_
#define NETWORK_S3D_

//#define IP_BROADCAST 		"192.168.1.255"
#define IP_BROADCAST 		"10.80.35.255"
#define IP_LISTENING 		"0.0.0.0"
#define PORT_BROADCAST 		50023
#define PORT_LISTENING 		50023

#define POSITION_PDU 	1
#define BULLET_PDU 		2
#define HIT_PDU 		3
#define KILL_PDU 		4
#define SCORE_PDU  		6


class NetworkManager {

	// UDP
	var net_channel;
	
	// Network parameters
	var host_id;
	
	// Stats
	var in_traffic;
	var out_traffic;

	init(id);
	updateInTrafficStats(time, size);
	updateOutTrafficStats(time, size);
	craftPlayerPosPDU(pos, angle, visible, frame);
	craftBulletPosPDU(weap, bull_idx, pos, dir, life, uid);
	craftHitPDU(hitter, weap, bull_idx);
	craftKillPDU(killer);
	craftScorePDU(player, enemies);
	sendPDU(pdu);
	receivePDU();
};


/* Initialize the network manager */
function NetworkManager::init(id)
{
	host_id = id;
	out_traffic = 0;
	net_channel= NetCreateChannel(PORT_LISTENING, IP_LISTENING, VR_NO_BLOCKING);
}


/* Update stats for incoming traffic */
function NetworkManager::updateInTrafficStats(time, size)
{
	static var counter = 0;
	static var old_time = 0;
    
    // Compute network load stats every second or less
    if (time - old_time > 1000) {
    	in_traffic = counter;
    	old_time = time;
    	counter = 0;	
    }
    counter += size;
}


/* Update stats for outcoming traffic */
function NetworkManager::updateOutTrafficStats(time, size)
{
	static var counter = 0;
	static var old_time = 0;
    
    // Compute network load stats every second or less
    if (time - old_time > 1000) {
    	out_traffic = counter;
    	old_time = time;
    	counter = 0;	
    }
    counter += size;
}


/* Create a PDU for updates about the player position 
 * [0]-typePDU, [1]-sender, [2,3,4]-posXYZ, [5]-angle, [6]-visible/hidden, [7]-animFrame
 */
function NetworkManager::craftPlayerPosPDU(pos, angle, visible, frame)
{
	var pdu = vector(8);
	
	pdu[0] = POSITION_PDU;
	pdu[1] = host_id;
	pdu[2] = pos.x;
	pdu[3] = pos.y;
	pdu[4] = pos.z;
	pdu[5] = angle;
	pdu[6] = visible;
	pdu[7] = frame;

	return pdu;
}


/* Create a PDU for updates about the position of a bullet
 * [0]-typePDU, [1]-sender, [2]-weapon, [3]-bull_idx, [4,5,6]-posXYZ, [7,8,9]-dirXYZ, [10]-life, [11]-uniqueID
 */
 function NetworkManager::craftBulletPosPDU(weap, bull_idx, pos, dir, life, uid)
 {
 	var pdu = vector(12);
	
	pdu[0] = BULLET_PDU;
	pdu[1] = host_id;
	pdu[2] = weap;
	pdu[3] = bull_idx;
	pdu[4] = pos.x;
	pdu[5] = pos.y;
	pdu[6] = pos.z;
	pdu[7] = dir.x;
	pdu[8] = dir.y;
	pdu[9] = dir.z;
	pdu[10] = life;
	pdu[11] = uid;
	
	return pdu;	
 }


/* Create a PDU to notify the other players about you being hit. 
 * [0]-typePDU, [1]-player_hit, [2]-hitter, [3]-weapon, [4]-bull_idx
 */
function NetworkManager::craftHitPDU(hitter, weap, bull_idx)
{
	var pdu = vector(6);
	
	pdu[0] = HIT_PDU;
	pdu[1] = host_id;
	pdu[2] = hitter;
	pdu[3] = weap;
	pdu[4] = bull_idx;
	
	return pdu;
}


/* Create a PDU to notify the other players about your death.
 * [0]-typePDU, [1]-killed, [2]-killer
 */
function NetworkManager::craftKillPDU(killer)
{
	var pdu = vector(3);
	
	pdu[0] = KILL_PDU;
	pdu[1] = host_id;
	pdu[2] = killer;
	
	return pdu;
}

/* Create a PDU to share your scoreboard to synchronize it with the other players 
 * [0]-typePDU, [1]-sender, [2+2*i]-player_i_kills, [3+2*i]-player_i_deaths
 */
function NetworkManager::craftScorePDU(myplayer, enemies)
{
	var pdu = vector(2 + 2*len(enemies));
	
	pdu[0] = SCORE_PDU;
	pdu[1] = host_id;
	for (var i=0; i<len(enemies); i++) {
		if (i == host_id) {
			// my score
			pdu[2*i+2] = myplayer.kills;
			pdu[2*i+3] = myplayer.deaths;
			
		} else if (enemies[i].active) {
			// enemies score
			pdu[2*i+2] = enemies[i].kills;
			pdu[2*i+3] = enemies[i].deaths;
		} else {
			// mark as empty
			pdu[2*i+2] = -1;
			pdu[2*i+3] = -1;
		}
	}
	return pdu;
}


/* Broadcast a PDU over the network */
function NetworkManager::sendPDU(pdu)
{
	// Update stats
    updateOutTrafficStats(getTime(), len(pdu));
    
    // Send
	NetVarSendTo(IP_BROADCAST, PORT_BROADCAST, pdu);
}


/* Receive a PDU from the network */
function NetworkManager::receivePDU()
{
	var sender_ip = "...";
	var recv_data = NetVarReceiveFrom(net_channel, &sender_ip);
	
	if (recv_data != null && recv_data[1] != host_id) { 	// avoid your own data
		// Update stats
		updateOutTrafficStats(getTime(), len(recv_data));
		return recv_data;
	} else {
		return null;
	}
}

#endif
